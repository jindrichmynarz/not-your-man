PREFIX hint: <http://www.bigdata.com/queryHints#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX wd:   <http://www.wikidata.org/entity/>
PREFIX wdt:  <http://www.wikidata.org/prop/direct/>

SELECT DISTINCT ?thing ?label ?property ?notyourman
WHERE {
  hint:Query hint:optimizer "None" .
  {
    SELECT ?thing ?label ?property ?object
    WHERE {
      {
        SELECT ?thing ?label (struuid() AS ?index)
        WHERE {
          {
            SELECT ?thing ?label
            WHERE {
              ?thing wdt:P279 wd:CLASS ;
                wdt:P276|wdt:P279|wdt:P366|wdt:P462|wdt:P495|wdt:P527|wdt:P1889|wdt:P2283 ?object ;
                rdfs:label ?label .

              FILTER (lang(?label) = "en")
            }
            GROUP BY ?thing ?label
            HAVING (COUNT(?object) > 3)
          }
        }
        ORDER BY ?index
        LIMIT 1
      }
      VALUES ?property {
              wdt:P276  # Location
              wdt:P279  # Subclass of
              wdt:P366  # Use
              wdt:P462  # Colour
              wdt:P495  # Country of origin
              wdt:P527  # Has part
              wdt:P1889 # Different from
              wdt:P2283 # Uses
      }
      ?thing ?property ?object .
    }
  }
  ?object rdfs:label ?notyourman .
  FILTER (lang(?notyourman) = "en" && ?notyourman != ?label)
}
LIMIT 5
