PREFIX hint: <http://www.bigdata.com/queryHints#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX wd:   <http://www.wikidata.org/entity/>
PREFIX wdt:  <http://www.wikidata.org/prop/direct/>

SELECT DISTINCT ?thing ?label ?property ?notyourman
WHERE {
  hint:Query hint:optimizer "None" .
  {
    SELECT ?thing ?label ?property ?object
    WHERE {
      {
        SELECT ?thing ?label (struuid() AS ?index)
        WHERE {
          {
            SELECT ?thing ?label
            WHERE {
              VALUES ?class {
                wd:Q212920 # Home applicance
                wd:Q2424752 # Product
                wd:Q3314483 # Fruit
                wd:Q39546 # Tool
                wd:Q43229 # Organization
                wd:Q815741 # Geometric shape
                wd:Q11004 # Vegetable
              }
              ?thing wdt:P279 ?class ;
                wdt:P276|wdt:P279|wdt:P366|wdt:P462|wdt:P495|wdt:P527|wdt:P1889 ?object ;
                rdfs:label ?label .

              FILTER (lang(?label) = "en"
                      &&
                      # Attempt to avoid proper nouns
                      ucase(substr(str(?label), 1, 1)) != substr(str(?label), 1, 1)
                      &&
                      # Attempt to avoid plurals
                      !strends(?label, "s"))
            }
            GROUP BY ?thing ?label
            HAVING (COUNT(?object) > 2)
          }
        }
        ORDER BY ?index
        LIMIT 1
      }
      VALUES ?property {
              wdt:P276  # Location
              wdt:P279  # Subclass of
              wdt:P366  # Use
              wdt:P462  # Colour
              wdt:P495  # Country of origin
              wdt:P527  # Has part
              wdt:P1889 # Different from
      }
      ?thing ?property ?object .
    }
  }
  ?object rdfs:label ?notyourman .
  FILTER (lang(?notyourman) = "en")
}
LIMIT 5
